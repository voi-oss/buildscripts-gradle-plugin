plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.19.0'
}

pluginBundle {
    website = project.POM_URL
    vcsUrl = project.POM_SCM_URL
    tags = ['build', 'ci', 'cd', 'android', 'deploy']
}

gradlePlugin {
    plugins {
        voiBuilScriptsPlugin {
            id = "$project.GROUP.$project.POM_ARTIFACT_ID"
            displayName = project.POM_NAME
            description = project.POM_DESCRIPTION
            implementationClass = 'buildscripts.VoiBuildScriptsPlugin'
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

Object getValue(String propertyName, boolean ignoreEmpty = false) {
    if (project.hasProperty(propertyName))
        return project.property(propertyName)

    String envProp = System.getenv(propertyName)
    if (envProp != null)
        return envProp

    if (ignoreEmpty)
        return ""
    else throw new IllegalStateException("Please make sure you defined $propertyName for this build!")
}

group = 'io.voiapp.android'
version = '0.8.9'

publishing {
    repositories {
        maven {
            credentials {
                username = getValue('voi_maven_user')
                password = getValue('voi_maven_password')
            }
            url 'https://maven.pkg.github.com/voiapp/mvn'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testImplementation 'junit:junit:4.12'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
}